plugins {
    id 'java'
    id 'maven'
}

repositories {
    mavenLocal()
    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

configurations {
    umlDoclet
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:2.1.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:2.1.4.RELEASE'
    compile 'org.postgresql:postgresql:42.2.5'
    compile 'org.projectlombok:lombok:1.18.6'
    runtime 'com.h2database:h2:1.4.199'
    testCompile 'org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE'

    umlDoclet 'nl.talsmasoftware:umldoclet:1.1.4' //support jdk 1.8
}

javadoc {
    source = sourceSets.main.allJava
    version = true
    options.author = true
    options.showAll()
    options.windowTitle = 'Domain Specification'
    options.docTitle = 'Domain Specification'
    options.bottom = 'Copyright 2019. All rights reserved.'
    options.group('Domain Packages', ['*.domain.*'])
    options.docletpath = configurations.umlDoclet.files.asType(List)
    options.doclet('nl.talsmasoftware.umldoclet.UMLDoclet')
    options.tags = [
            'plantuml.domainModuleName:t:PlantUml domain:',
            'plantuml.skip:tcmf',
            'note:t'
    ]
    options.addStringOption('umlIncludeConstructors', 'true')
    options.addStringOption('umlIncludePublicFields', 'true')
    options.addStringOption('umlIncludePrivateFields', 'true')
    options.addStringOption('umlIncludeProtectedFields', 'true')

    failOnError false
}

group = 'lt.asprogramuoju'
version = '0.0.1-SNAPSHOT'
description = 'session-hiber'
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
